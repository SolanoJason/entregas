plugins {
    id 'java'
    id 'application'
    id 'com.github.ben-manes.versions' version '0.33.0'
    id 'com.github.johnrengelman.shadow' version '6.1.0'
    id 'de.inetsoftware.setupbuilder' version '4.8.7'
    id 'edu.sc.seis.launch4j' version '2.4.8'
}

java {
    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8
}

compileJava {
    options.incremental = true
    options.fork = true
    options.failOnError = false
}

// Aca indicamos la ubicaci�n del main
application {
    applicationName = 'Proyecto en aula Programación 3'
    mainClassName = 'ventanas.Menu'
}

group = 'com.grupodos'
version = '1.0.0.1'

repositories {
    mavenCentral()
    maven {
        url = uri('https://oss.sonatype.org/content/repositories/comgithublgooddatepicker-1026')
    }
    jcenter()
    maven {
        url = uri('http://jaspersoft.jfrog.io/jaspersoft/third-party-ce-artifacts/')
    }
}

test {
    useJUnitPlatform()
}

dependencies {
    // Esto es para unit tests, aun no se va a usar
    testImplementation 'org.junit.jupiter:junit-jupiter:5.7.0'

    // logger, esto es super super util  frontend 
    implementation 'com.google.flogger:flogger:0.5.1' 
    //backend logger
    implementation 'com.google.flogger:flogger-log4j2-backend:0.5.1' 
    implementation 'org.apache.logging.log4j:log4j-core:2.13.3'
    implementation 'org.apache.logging.log4j:log4j-slf4j-impl:2.13.3'

    //Para el skin, hay un montón de skins para escoger
    // https://github.com/JFormDesigner/FlatLaf/tree/master/flatlaf-intellij-themes#themes
    implementation 'com.formdev:flatlaf:0.43'
    implementation 'com.formdev:flatlaf-intellij-themes:0.43'
    implementation 'com.formdev:flatlaf-extras:0.43'
    implementation 'com.formdev:svgSalamander:1.1.2.3' 

    // Para usar calendario
    implementation 'com.github.lgooddatepicker:LGoodDatePicker:11.1.0'


    //Hace posible  y no llorar sangre  escribir interfaces graficas a mano
    implementation 'com.miglayout:miglayout-core:5.2' 
    implementation 'com.miglayout:miglayout-swing:5.2'

    //Conector para mysql
    implementation 'mysql:mysql-connector-java:8.0.21'
    // db pool
    implementation 'com.zaxxer:HikariCP:3.4.5'
    // connector para maria
//    implementation 'org.mariadb.jdbc:mariadb-java-client:2.7.0'

    //Guava
    //Porque guava?
    // https://github.com/google/guava/wiki
    implementation 'com.google.guava:guava:29.0-jre' 

    // Para cargar la librería de Jasper
    implementation 'net.sf.jasperreports:jasperreports:6.15.0'

    // Material icons
    implementation 'com.github.jiconfont:jiconfont-swing:1.0.1'
    implementation 'com.github.jiconfont:jiconfont-font_awesome:4.7.0.1'
    implementation 'com.github.jiconfont:jiconfont-google_material_design_icons:2.2.0.2'
}

setupBuilder {
    vendor = 'Grupo 1'
    application = 'G1P3'
    appIdentifier = 'ProjectoP3'
    description = 'La aplicacion resultante del projecto de Programacion 3 - Ciclo 2020-1'
    licenseFile "license.txt"
    icons = ["${projectDir}/src/main/resources/icons/iconounasam.png"]

//    from shadowJar.outputs

//    bundleJre = 'C:\\Program Files\\AdoptOpenJDK\\jdk-11.0.8.10-hotspot'
//    mainClass = 'ventanas.Menu'
//    mainJar = 'Programa1-1.0.0.1-all.jar'

    desktopStarter {
        displayName = 'Projecto Entrega de dispositivos - P3 2020-1'
        description   = "Aplicacion desarrollada para la entrega de dispositivos"
    }
}

createExe.dependsOn ':copyShadow'

task copyShadow (type: Copy) {
    dependsOn shadowJar
    from "${project.tasks.shadowJar.archivePath}"
    into "${buildDir}/launch4j/lib/"
}


launch4j {

    mainClassName = project.mainClassName
    icon = "${projectDir}/src/main/resources/icons/iconounasam.ico"
    dontWrapJar = false
    headerType = 'gui'
    downloadUrl = 'https://adoptopenjdk.net/index.html'
//    bundledJre64Bit = true
//    bundledJreAsFallback = true
//    bundledJrePath = '1.8'
    jar = "lib/${project.tasks.shadowJar.archiveName}"
    copyConfigurable = ["${project.tasks.shadowJar.archivePath}"]
    outfile = 'Entrega_Dipositivos_P3.exe'
}

msi {
    setLanguages('es-es')
    minOS = 6.1
    from( createExe.outputs ) {
        include 'Entrega_Dipositivos_P3.exe'
    }
}

msi.dependsOn createExe

shadowJar{
//  minimize()
}